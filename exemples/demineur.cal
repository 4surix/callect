// Callect v1.0.0-pre6
   ---------------------------------------------------------------------------
   Démineur.
//

ANSI = import{'ansi'}

print.end__ = printANSI.end__ = ''

NBR_MINES = 5

NBR_PARTIES:pos = 0

HAUTEUR = 5
LONGUEUR = 10

BORD_HAUT = 7
BORD_BAS = BORD_HAUT + HAUTEUR + 1
BORD_GAUCHE = 1
BORD_DROITE = BORD_GAUCHE + LONGUEUR + 1

@'MAZ' {} [
    printANSI{
        ANSI.CLEAR.screen{2}
        + ANSI.CURSOR.move{1, 1}
        + ANSI.FG.yellow
        + '[DEMINEUR]'
        + ANSI.FG.reset
        + ANSI.CURSOR.move{1, 3}
        + ANSI.FG.blue
        + 'PARTIE n°' + txt{NBR_PARTIES{}}
        + ANSI.FG.reset
        + ANSI.CURSOR.move{1, 5}
        + '0/' + NBR_MINES:txt + ' drapeaux'
    }

    global X = BORD_GAUCHE + 1
    global Y = BORD_HAUT + 1

    global DRAPEAUX = {}
    global MINE_DECOUVERT = {}

    GEN_GRILLE{}
    PLACE_MINES{}

    print{'', 'x'=X, 'y'=Y}
]

@'PERDU' {} [
    printANSI{
        ANSI.CURSOR.move{1, 3}
        + ANSI.CLEAR.line{2}
        + ANSI.FG.red
        + 'PERDU !'
        + ANSI.FG.reset
    }
    stop{3}
    MAZ{}
]

@'GAGNER' {} [
    printANSI{
        ANSI.CURSOR.move{1, 3}
        + ANSI.CLEAR.line{2}
        + ANSI.FG.green
        + 'GAGNER !'
        + ANSI.FG.reset
    }
    stop{3}
    MAZ{}
]

//
    Grille
//

GRILLE = {}

@'GEN_GRILLE' {} [
    for x in 1;LONGUEUR [
        GRILLE # x = AXE_Y = {}
        repeat HAUTEUR [
            AXE_Y.add{0}
        ]
    ]

    print{'o' 
          + '-' * LONGUEUR
          + 'o'
          + '\n'
          +( '|'
             + ' ' * LONGUEUR
             + '|'
             + '\n'
           ) * HAUTEUR
          + 'o' 
          + '-' * LONGUEUR
          + 'o'

        'x'=BORD_GAUCHE, 
        'y'=BORD_HAUT
    }
]

//
    Mines
//

@'PLACE_MINES' {} [
    repeat NBR_MINES [
        x = random{1, LONGUEUR}, y = random{1, HAUTEUR}
        up GRILLE # x # y
        GRILLE # x # y = 'MINE'
    ]
]

//
    Position
//

X = Y = 0

event keypress 'Up' [
    down Y == BORD_HAUT + 1
    global Y = Y - 1
    print{'', 'x'=X, 'y'=Y}
]
event keypress 'Down' [
    down Y == BORD_BAS - 1
    global Y = Y + 1
    print{'', 'x'=X, 'y'=Y}
]
event keypress 'Left' [
    down X == BORD_GAUCHE + 1
    global X = X - 1
    print{'', 'x'=X, 'y'=Y}
]
event keypress 'Right' [
    down X == BORD_DROITE - 1
    global X = X + 1
    print{'', 'x'=X, 'y'=Y}
]

//
    Commandes
//

@'place_nbr_mines' {x, y} [

    // Enlève le drapeau si existe
    //
    coordonnees = {x, y}

    if coordonnees in DRAPEAUX [
        DRAPEAUX.rem{coordonnees}
        print{' ', 'x' = X, 'y' = Y}

        if GRILLE # x # y == 'MINE' & coordonnees in MINE_DECOUVERT [
            MINE_DECOUVERT.rem{coordonnees}
        ]
    ]

    GRILLE # x # y = 'DECOUVERT'

    // Définition de la zone de recherche
       OOO
       OXO
       OOO
       X = {x, y}
    //
    x_min = x - 1    y_min = y - 1
    x_max = x + 1    y_max = y + 1

    if x_min < 1 [x_min = 1]
    if y_min < 1 [y_min = 1]

    if x_max > LONGUEUR [x_max = LONGUEUR]
    if y_max > HAUTEUR  [y_max = HAUTEUR]

    // Récupére les mines autours de la case
    //
    nombre_mine:pos = 0

    for x__ in x_min ; x_max [
        for y__ in y_min ; y_max [
            if GRILLE # x__ # y__ == 'MINE' [
                nombre_mine{}
            ]
        ]
    ]

    // Si il n'y a aucune mine autour
         dévoile les autres cases autours.
    //
    if nombre_mine == 0 [
        for x__ in x_min ; x_max [
            for y__ in y_min ; y_max [
                if !(GRILLE # x__ # y__ == 'DECOUVERT') [
                    print{
                       place_nbr_mines{x__, y__},
                       'x' = x__ + BORD_GAUCHE,
                       'y' = y__ + BORD_HAUT
                   }
               ]
            ]
        ]
        return '.'
    ]

    return nombre_mine
]

event keypress 'Return' [

    x = X - BORD_GAUCHE
    y = Y - BORD_HAUT

    if GRILLE # x # y == 'MINE' [
        // Si c'était une mine
        //
        PERDU{}
    ] else [
        // Indique le nombre de mine autour de la case
        //
        print{place_nbr_mines{x, y}, 'x' = X, 'y' = Y}
        print{'', 'x' = X, 'y' = Y}
    ]
]


DRAPEAUX = {}
MINE_DECOUVERT = {}

event keypress ' ' [

    x = X - BORD_GAUCHE
    y = Y - BORD_HAUT
    coordonnees = {x, y}

    if coordonnees in DRAPEAUX [
        DRAPEAUX.rem{coordonnees}
        print{' ', 'x' = X, 'y' = Y}

        if GRILLE # x # y == 'MINE' & coordonnees in MINE_DECOUVERT [
            MINE_DECOUVERT.rem{coordonnees}
        ]
    ] else [
        DRAPEAUX.add{coordonnees}
        print{'!', 'x' = X, 'y' = Y}

        if GRILLE # x # y == 'MINE' & !(coordonnees in MINE_DECOUVERT) [
            MINE_DECOUVERT.add{coordonnees}
        ]
    ]

    print{txt{len{DRAPEAUX}} + '/' + NBR_MINES:txt + ' drapeaux', 'x'=1, 'y'=5}
    print{'', 'x'=X, 'y'=Y}

    if len{MINE_DECOUVERT} == NBR_MINES [
        GAGNER{}
    ]
]

MAZ{}

// Arrêt de 30 minutes
   Pour éviter que le programme se fini et coupe les events
//
stop{30 * 60}
