
ANSI = import{'ansi'}


print.end__ = printANSI.end__ = ''
print.sep__ = printANSI.sep__ = ''

X = Y = X_POMME = Y_POMME = PRESS_ENTRY = 0


// MAP //

MAP = " | Score: 000
\n
\nX X X X X X X X X X X X X
\nX                       X
\nX                       X
\nX                       X
\nX                       X
\nX                       X
\nX                       X
\nX                       X
\nX                       X
\nX                       X
\nX                       X
\nX                       X
\nX X X X X X X X X X X X X
"

BORD_haut   = 3
BORD_bas    = len{MAP.split{'\n'}}
BORD_gauche = 1
BORD_droite = len{MAP.split{'\n'} # BORD_haut} - 1


// SERPENT //

@'SUPR_FIN_QUEUX' [
	coordonnées = SERPENT.pop{1}
	x = coordonnées # 1
	y = coordonnées # 2
	print{' ', 'x'=x, 'y'=y, 'reset'=1}

	if x == X_POMME & y == Y_POMME [
	    print{'6', 'x'=X_POMME, 'y'=Y_POMME, 'reset'=1}
	]
]

@'AFF_DEBUT_QUEUX' [
	SERPENT.add{{X, Y}}
	print{'o', 'x'=X, 'y'=Y, 'reset'=1}
]


// POMME //

MIN_Y_POMME = BORD_haut + 1
MAX_Y_POMME = BORD_bas - 1
MIN_X_POMME = BORD_gauche + 1
MAX_X_POMME = BORD_droite / 2

event changevars (X == X_POMME & Y == Y_POMME) [
	// L'orsque la tête du serpent se trouve sur la pomme
	//

	SERPENT.insert{1, SERPENT#1}


	while Y_POMME == Y [hide Y_POMME = random{MIN_Y_POMME, MAX_Y_POMME}]
	while X_POMME == X [hide X_POMME = random{MIN_X_POMME, MAX_X_POMME} * 2]

	print{'6', 'x'=X_POMME, 'y'=Y_POMME, 'reset'=1}


	score:txt = SCORE{}

	if SCORE < 10  [score = '0' + score]
	if SCORE < 100 [score = '0' + score]

	print{score, 'x'=11, 'y'=1, 'reset'=1}
]


// DIRECTION //

event keypress 'Up'    [DIRECTION = 'N']
event keypress 'Down'  [DIRECTION = 'S']
event keypress 'Left'  [DIRECTION = 'O']
event keypress 'Right' [DIRECTION = 'E']


// PARTIE //

print{"Appuyez sur Entrée pour commencer !"}


event keypress 'Return' [PRESS_ENTRY = 1]

while 1 [

	while !PRESS_ENTRY [stop{1}]

	PRESS_ENTRY = 0, printANSI{ANSI.CLEAR.screen{2}}


	// Remise des valeurs à zéro //

	print{MAP}

	DIRECTION = 'S'

	SCORE:pos = 0

	X = BORD_gauche + 1
	Y = BORD_haut + 1

	Y_POMME = random{MIN_Y_POMME, MAX_Y_POMME}
	X_POMME = random{MIN_X_POMME, MAX_X_POMME} * 2

	print{'6', 'x'=X_POMME, 'y'=Y_POMME}

	SERPENT = {{X, Y}}


	// Lancement partie //

	PERDU = 0

	while !PERDU [

		if DIRECTION == 'N' [
			Y =  Y - 1, if Y == BORD_haut [PERDU = 1]
		]
		elif DIRECTION == 'S' [
			Y = Y + 1, if Y == BORD_bas [PERDU = 1]
		]
		elif DIRECTION == 'O' [
			X = X - 2, if X <= BORD_gauche [PERDU = 1]
		]
		elif DIRECTION == 'E' [
			X = X + 2, if X >= BORD_droite [PERDU = 1]
		]

		SUPR_FIN_QUEUX

		if {X, Y} in SERPENT [
		    PERDU = 1
		]

		AFF_DEBUT_QUEUX

		stop{0.3}
	]

	printANSI{
	    ANSI.CURSOR.move{1, 2}
	    + ANSI.FG.red
	    + 'Vous avez perdu !'
	    + ANSI.FG.reset
	}
]
